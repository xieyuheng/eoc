#!/usr/bin/env -S npx @xieyuheng/x-lisp.js run --debug=true

(import-all "src/index.lisp")

(match (current-command-line-args)
  (['compile input :output output :optimization-level optimization-level]
   (= input-file (symbol-to-string input))
   (= program (load-program input-file))
   (= x86-program (compile-program program optimization-level))
   (= output-file (if (null? output)
                    (string-append input-file ".exe")
                    (symbol-to-string output)))
   (generate-exe x86-program output-file))

  (['compile-assembly input :optimization-level optimization-level]
   (= input-file (symbol-to-string input))
   (= program (load-program input-file))
   (= x86-program (compile-program program optimization-level))
   (write (format-x86-program x86-program)))

  (['compile-passes input]
   (= input-file (symbol-to-string input))
   (compile-passes (load-program input-file)))

  (['help]
   (print-help))

  (_
   (print-help)))

(define runtime-file
  (path-join [(current-module-directory) "../runtime/runtime.o"]))

(define tmp-directory "/tmp/eoc")

(define (generate-exe x86-program output-file)
  (= output-id (string-replace "/" "-" output-file))
  (= asm-id (string-append-many
             [output-id "." (format-sexp (random-int 0 10000)) ".s"]))
  (= asm-file (path-join [tmp-directory asm-id]))
  (directory-create-recursively (file-directory asm-file))
  (file-save asm-file (format-x86-program x86-program))
  (= [:exit-code exit-code :stdout stdout :stderr stderr]
     (system-shell-run "cc" [asm-file runtime-file "-o" output-file]))
  (if (equal? exit-code 0)
    (file-remove asm-file)
    (begin
      (write stderr)
      (exit [:who 'generate-exe
                  :message "c compiler fail"
                  :output-file output-file]))))

(define (load-program file)
  (parse-program
   `(program
     ()
     ,(parse-sexp (file-load file)))))

(define (print-help)
  (writeln "commands:")
  (writeln "  compile-assembly <file> -- compile a file to assembly")
  (writeln "  compile-passes <file> -- output all compiler passes for snapshot testing")
  (writeln "  help -- display help information")
  (writeln "")
  (writeln "default command: help"))
